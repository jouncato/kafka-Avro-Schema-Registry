plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
}

group = 'com.litethinking'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'com.litethinking.kafka.WikimediaStreamingApplication'
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.apache.avro:avro:1.11.3'
    implementation 'io.confluent:kafka-avro-serializer:7.5.1'
    implementation 'io.confluent:kafka-schema-registry-client:7.5.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation('com.github.javafaker:javafaker:1.0.2') {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

avro {
    stringType = "String"
    fieldVisibility = "PRIVATE"
    createSetters = true
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src/avro/main']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}